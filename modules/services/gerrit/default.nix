{
  config,
  pkgs,
  lib,
  ...
}:
let
  cfg = config.services.gerrit;

  fname = i: extra: "${toString i}-${builtins.baseNameOf extra}";
  creds-args = lib.imap (i: extra: "${fname i extra}:${toString extra}") cfg.extraConfigFiles;
  creds-cmds = lib.imap (
    i: extra: ''cat "''$CREDENTIALS_DIRECTORY/${fname i extra}" >>  etc/gerrit.config''
  ) cfg.extraConfigFiles;
in
{
  options.services.gerrit = {
    extraConfigFiles = lib.mkOption {
      description = ''
        Extra files content added to gerrit.config. May contain path to secret
        generated by encryption nix tools like agenix files or paths
        non-managed by config.
      '';
      type = with lib.types; listOf (either str path);
      default = [ ];
    };
    listenPort = lib.mkOption {
      description = ''
        Number of local port to listen by gerrit.
      '';
      type = lib.types.port;
      default = 8080;
    };
  };
  config = {
    services.gerrit = {
      enable = config.hardware.isVps;
      plugins = with pkgs; [
        gerrit-oauth-plugin
        gerrit-admin-console-plugin
      ];
      settings = {
        auth.type = "oauth";
        httpd.listenUrl = "proxy-https://localhost:${toString cfg.listenPort}";
        gerrit.canonicalWebUrl = "https://${config.services.vps.domain}";
      };
    };
    systemd.services.gerrit = {
      preStart = lib.mkAfter ''
        cp -L --no-preserve=mode etc/{,_}gerrit.config
        rm etc/gerrit.config
        mv etc/{_,}gerrit.config

        ${lib.concatStringsSep "\n" creds-cmds}
      '';
      serviceConfig.LoadCredential = creds-args;
    };
    networking.firewall.allowedTCPPorts = lib.mkIf config.services.gerrit.enable [
      (
        # (toPortNumber services.gerrit.settings.sshd.listenAddress) or # TODO
        29418
      )
    ];
  };
}
